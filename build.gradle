buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.3.4'
    }
}

group TOOLBELT_ORG
version TOOLBELT_VERSION

apply plugin: 'java'
apply plugin: 'com.novoda.bintray-release'

targetCompatibility = JavaVersion.VERSION_1_7
sourceCompatibility = JavaVersion.VERSION_1_7

repositories {
    mavenCentral()
}

publish {
    userOrg = hasProperty('BINTRAY_USER') ? BINTRAY_USER : System.getenv('BINTRAY_USER')
    groupId = TOOLBELT_ORG
    artifactId = TOOLBELT_NAME
    publishVersion = TOOLBELT_VERSION
    desc = TOOLBELT_DESC
    website = TOOLBELT_WEBSITE
    bintrayUser = hasProperty('BINTRAY_USER') ? BINTRAY_USER : System.getenv('BINTRAY_USER')
    bintrayKey = hasProperty('BINTRAY_KEY') ? BINTRAY_KEY : System.getenv('BINTRAY_KEY')
}

subprojects {
    group = TOOLBELT_ORG
    version = TOOLBELT_VERSION

    if (IS_UPLOADING && project.name in [MODULE_NAME]) {
        println project.name
        apply plugin: 'maven'

        gradle.taskGraph.whenReady { taskGraph ->
            taskGraph.getAllTasks().find {
                it.path == ":$project.name:generatePomFileForMavenPublication"
            }.doLast {
                file("build/publications/maven/pom-default.xml").delete()
                println 'Overriding pom-file to make sure we can sync to maven central!'
                pom {
                    //noinspection GroovyAssignabilityCheck
                    project {
                        name "$project.name"
                        artifactId TOOLBELT_NAME
                        packaging project.name == 'compiler' ? 'jar' : 'aar'
                        description DESCRIPTION
                        url TOOLBELT_WEBSITE
                        version TOOLBELT_VERSION

                        scm {
                            url TOOLBELT_WEBSITE
                            connection TOOLBELT_WEBSITE
                            developerConnection TOOLBELT_WEBSITE
                        }

                        licenses {
                            license {
                                name 'Apache v2'
                            }
                        }

                        developers {
                            developer {
                                id DEVELOPER_ID
                                name DEVELOPER_NAME
                                email DEVELOPER_EMAIL
                            }
                        }
                    }
                }.writeTo("build/publications/maven/pom-default.xml")
            }
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'

    compile "org.jetbrains:annotations:13.0"
}
